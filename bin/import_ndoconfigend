#!/usr/bin/perl
#
#
# SYNTAX:
#	import_ndoconfigend
#
# DESCRIPTION:
#	Processes ndoutils_configdumpend jobs asynchronously to other NDO updates
#	Allows DB updates to continue while this post reload job runs
#	Will take actions from the var/ndoconfigend directory
#	in filename order but will ignore if more than 1 running
#   This is only a temporary measure until we get import_ndologsd to do the NDO processing
#   itself
#
# AUTHORS:
#	Copyright (C) 2003-2013 Opsview Limited. All rights reserved
#
#    This file is part of Opsview
#
#    Opsview is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    Opsview is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with Opsview; if not, write to the Free Software
#    Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
#

use warnings;
use strict;
use FindBin qw($Bin);
use lib $Bin, "$Bin/../lib", "$Bin/../etc", "$Bin/../perl/lib";
use Log::Log4perl;
use POSIX;
use Time::HiRes qw(sleep gettimeofday);

$0 = "import_ndoconfigend";

my $rootdir    = "/usr/local/nagios";
my $import_dir = "$rootdir/var/ndoconfigend";

Log::Log4perl::init_and_watch( "$rootdir/etc/Log4perl.conf", 30 );

my $logger = Log::Log4perl->get_logger( "import_ndoconfigend" );

$logger->info( "Starting" );

my $pidfile = "/usr/local/nagios/var/import_ndoconfigend.pid";
if ( -e $pidfile ) {
    open( my $fh, "< $pidfile" )
      or $logger->logdie( "Cannot read pid file - is daemon running?" );
    my $pid = <$fh>;
    close $fh;
    if ( kill 0, $pid ) {
        $logger->logdie( "import_ndoconfigend already running as process $pid"
        );
    }
    else {
        $logger->warn( "Crash from previous run. Deleting pid file" );
        unlink $pidfile or $logger->logdie( "Cannot delete pidfile" );
    }
}

if (1) {
    my $pid = fork;
    exit if $pid;
    $logger->logdie("Couldn't fork: $!") unless defined $pid;
    POSIX::setsid() or $logger->logdie( "Cannot daemonize" );

    $logger->info( "Daemonised" );

    open PID, "> $pidfile" or die "Could not write to pid file $pidfile";
    print PID "$$";
    close PID;

    for my $handle ( *STDIN, *STDOUT, *STDERR ) {
        open( $handle, "+<", "/dev/null" )
          || die "can't reopen $handle to /dev/null: $!";
    }
}

my $break = 0;
$SIG{INT}  = \&catch_signals;
$SIG{TERM} = \&catch_signals;

chdir($import_dir) or $logger->logdie( "Cannot chdir: $!" );
my $last_update = 0;
my $c           = 0;
MAINLOOP:
until ($break) {

    # Not sure why, but can't use IMPORT_DIR filehandle
    my $dir_update = ( stat(".") )[9];
    if ( $dir_update != $last_update ) {
        $last_update = $dir_update;

        opendir( IMPORT_DIR, "." ) or $logger->logdie( "Cannot open dir: $!" );
        my @files = sort ( grep !/^\.\.?\z/, readdir IMPORT_DIR );
        closedir IMPORT_DIR;

        next unless @files;

        $logger->info( "Processing ndoconfigend files: " . scalar @files );
        process_jobs(@files);
    }

    # Directory would have changed from our last import, so expect above if to succeed
    # But allows a next check to run if more files have been added
    sleep 1;
}

cleanup();

sub cleanup {
    $logger->info( "Stopping $0" );
    unlink $pidfile;
}

sub catch_signals {
    if ( $break++ == 0 ) {
        $logger->warn( "Received kill signal - gracefully shutting down" );
    }
    else {
        $logger->logdie( "Received kill signal - forced death" );
    }
}

sub process_jobs {
    my @files = @_;

    my $last_reload_time = 0;

    # Delete any other files, because we only process once if there's a whole queue of them
    for my $f (@files) {

        my $mtime = ( stat($f) )[9];
        $last_reload_time = $mtime if $mtime > $last_reload_time;

        unlink $f;
    }

    system( "/usr/local/nagios/bin/ndoutils_configdumpend", $last_reload_time );
}
