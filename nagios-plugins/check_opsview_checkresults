#!/usr/bin/perl
#
#
# SYNTAX:
#	check_opsview_checkresults
#
# DESCRIPTION:
#	On master server makes sure nagios process is keeping up to date
#	by ensuring nothing is older than 60 seconds within checkresukts
#   directory.  Also report stats.
#
# AUTHORS:
#	Copyright (C) 2003-2013 Opsview Limited. All rights reserved
#
#    This file is part of Opsview
#
#    Opsview is free software; you can redistribute it and/or modify
#    it under the terms of the GNU General Public License as published by
#    the Free Software Foundation; either version 2 of the License, or
#    (at your option) any later version.
#
#    Opsview is distributed in the hope that it will be useful,
#    but WITHOUT ANY WARRANTY; without even the implied warranty of
#    MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#    GNU General Public License for more details.
#
#    You should have received a copy of the GNU General Public License
#    along with Opsview; if not, write to the Free Software
#    Foundation, Inc., 51 Franklin St, Fifth Floor, Boston, MA  02110-1301  USA
#

use strict;
use FindBin qw($Bin);
use lib "$Bin/../lib", "$Bin/../etc", "$Bin/../perl/lib";
use Nagios::Plugin;
use File::stat;

my $VERSION = "0.01";

my $np = Nagios::Plugin->new(
    usage =>
      "Usage: %s [-v|--verbose] [-h|--help] [-w, --warning=<seconds>] [-c, --critical=<seconds>]\n   Checks data is being imported nagios",
    shortname => "CRD",
    version   => $VERSION,
);

$np->add_arg(
    spec => "warning|w=s",
    help => qq{-w, --warning=INTEGER
   Minimum and maximum number of allowable result, outside of which a
   warning will be generated (default: 30)},
    default => 30,
);

$np->add_arg(
    spec => "critical|c=s",
    help => qq{-c, --critical=INTEGER
   Minimum and maximum number of the generated result, outside of
   which a critical will be generated (default: 60) },
    default => 60,
);

$np->getopts;

alarm $np->opts->timeout;

#Â We make this a "require" otherwise errors occur on fresh systems as instance.cfg doesn't exist yet
require Opsview::Slave::Config;
my $import_dir = Opsview::Slave::Config->check_result_path;

if ( !-d $import_dir ) {
    $np->nagios_exit( CRITICAL, "checkresults directory does not exist" );
}

opendir( IMPORT, $import_dir );

# only interested in the oldest file
my $oldest_file_name  = '';
my $oldest_file_age   = time();
my $largest_file_size = 0;
my $largest_file_name = 0;
my $total_file_count  = 0;

while ( my $file = readdir(IMPORT) ) {
    next if ( $file =~ m/^\.+$/ );

    # wrapped in an eval just in case file is removed before it can be checked
    my $filestat = eval { stat( $import_dir . "/" . $file ) };
    next unless ($filestat);

    $total_file_count++;

    if ( $filestat->mtime < $oldest_file_age ) {
        $oldest_file_age  = $filestat->mtime;
        $oldest_file_name = $file;
    }

    if ( $filestat->size > $largest_file_size ) {
        $largest_file_name = $file;
        $largest_file_size = $filestat->size;
    }
}
closedir(IMPORT);

my $result = time() - $oldest_file_age;

$np->add_perfdata(
    label    => "last_import",
    uom      => "s",
    value    => $result,
    warning  => $np->opts->warning,
    critical => $np->opts->critical,
);

# log the number of backlogged files with a SWAG at warning/crit values
$np->add_perfdata(
    label    => "checkresults_file_backlog",
    uom      => "",
    value    => $total_file_count,
    warning  => 1000,
    critical => 10000,
);

# log the largest file size
$np->add_perfdata(
    label => "checkresults_largest_size",
    uom   => "",
    value => $largest_file_size,
);

$np->nagios_exit(
    return_code => $np->check_threshold($result),
    message =>
      "oldest checkresult file is $result seconds old, $total_file_count checkresult files backlogged",
);
