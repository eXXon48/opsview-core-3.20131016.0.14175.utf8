#!/usr/bin/perl -w

#  Copyright (C) 2003-2013 Opsview Limited. All rights reserved
#  W: http://www.opsview.com/
#
#  This program is free software; you can redistribute it and/or modify
#  it under the terms of the GNU General Public License as published by
#  the Free Software Foundation; either version 2 of the License, or
#  (at your option) any later version.
#
#  This program is distributed in the hope that it will be useful,
#  but WITHOUT ANY WARRANTY; without even the implied warranty of
#  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
#  GNU General Public License for more details.
#
#  You should have received a copy of the GNU General Public License
#  along with this program; if not, write to the Free Software
#  Foundation, Inc., 59 Temple Place, Suite 330, Boston, MA  02111-1307  USA

use warnings;
use strict;
use FindBin qw($Bin $Script);
use lib "$Bin/../perl/lib";
use lib '/usr/local/nagios/perl/lib';
use lib '/usr/local/nagios/lib';
use List::Util qw(sum max min);
use Nagios::Plugin;
use Nagios::Plugin::Functions qw(max_state %STATUS_TEXT);

my $VERSION = "1.0";

my $np = Nagios::Plugin->new(
    usage   => 'Usage: %s [-w warning] [-c critical] <database name>',
    version => $VERSION,
    blurb =>
      'Check for allocated but unused tablespace in the specified Opsview '
      . 'database, indicating the table may need optimising.',
);
$np->add_arg(
    spec => 'warning|w=s',
    help =>
      'Alert warning if unused spaces is above this limit (default: 20480 kilobytes)',
    default => 20480,
);
$np->add_arg(
    spec => 'critical|c=s',
    help =>
      'Alert critical if unused spaces is above this limit (default: 40960 kilobytes)',
    default => 40960,
);

$np->getopts;

my $database = shift @ARGV;
$np->shortname( uc( $database . '_FREE' ) );

$np->set_thresholds(
    warning  => $np->opts->warning,
    critical => $np->opts->critical
);

$np->nagios_exit(
    return_code => UNKNOWN,
    message     => 'No database specified',
) unless $database;

$np->nagios_exit(
    return_code => UNKNOWN,
    message     => 'No database specified',
) unless $database =~ /(?:opsview|runtime|odw|reports)/i;

$database = ucfirst($database);
eval "require $database";
if ($@) {
    $np->nagios_exit(
        return_code => UNKNOWN,
        message     => $@,
    );
}

my $dbh = $database->db_Main;
my $sth = $dbh->prepare( 'SHOW TABLE STATUS' );
$sth->execute;

my $total_free_space;
my $total_used_space;
while ( my $row = $sth->fetchrow_hashref ) {
    $total_free_space += $row->{data_free} / 1024;
    $total_used_space += $row->{data_length} / 1024;
    my $state = $np->check_threshold( check => $row->{data_free} / 1024 );
    if ( $state != OK ) {
        $np->add_message(
            $state,
            $row->{name} . ' -> '
              . to_2_decimal_points( $row->{data_free} / 1024 )
              . ' kilobytes free',
        );
    }
}

# no point in including thresholds since they are per table and this total
$np->add_perfdata(
    label => 'total_free_space',
    value => to_2_decimal_points($total_free_space),
    uom   => 'KB',
);

$np->nagios_exit( $np->check_messages, );

sub to_2_decimal_points {
    return sprintf( '%.2f', shift );
}
